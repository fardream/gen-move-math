module example::uint16 {
    struct Uint16 has store, copy, drop {
        hi: u8,
        lo: u8,
    }

    const FULL_SIZE: u8 = 16;
    const UNDERLYING_SIZE: u8 = 8;
    const UNDERLYING_HALF_SIZE: u8 = 4;
    const UNDERLYING_HALF_POINT: u8 = 128;
    const UNDERLYING_LOWER_ONES: u8 = 15;
    const UNDERLYING_UPPER_ONES: u8 = 240;
    const UNDERLYING_ONES: u8 = 255;

    const E_OVERFLOW: u64 = 1001;
    const E_DIVISION_BY_ZERO: u64 = 1002;

    // new creates a new Uint16
    public fun new(hi: u8, lo: u8): Uint16 {
        Uint16 {
            hi, lo,
        }
    }

    public fun zero(): Uint16 {
        Uint16 {
            hi: 0,
            lo: 0,
        }
    }

    public fun equal(x: Uint16, y: Uint16): bool {
        x.hi == y.hi && x.lo == y.lo
    }

    public fun greater(x: Uint16, y: Uint16): bool {
        (x.hi > y.hi) || ((x.hi == y.hi) && (x.lo > y.lo))
    }

    public fun less(x: Uint16, y: Uint16): bool {
        (x.hi < y.hi) || ((x.hi == y.hi) && (x.lo < y.lo))
    }

    // add two underlying with carray - will never abort.
    // First return value is the value of the result, the second return value indicate if carry happend.
    fun underlying_add_with_carry(x: u8, y: u8):(u8, u8) {
        let r = UNDERLYING_ONES - x;
        if (r < y) {
            (y - r - 1, 1)
        } else {
            (x + y, 0)
        }
    }

    // subtract y from x with borrow - will never abort.
    // First return value is the value of the result, the second return value indicate if borrow happend.
    fun underlying_sub_with_borrow(x: u8, y:u8): (u8, u8) {
        if (x < y) {
            ((UNDERLYING_ONES - y) + 1 + x, 1)
        } else {
            (x-y, 0)
        }
    }

    // add x and y, plus possible carry, abort when overflow
    fun underlying_add_plus_carry(x: u8, y: u8, carry: u8): u8 {
        x + y + carry // will abort if overflow
    }

    // subtract y and possible borrow from x, abort when underflow
    fun underlying_sub_minus_borrow(x: u8, y: u8, borrow: u8): u8 {
        x - y - borrow // will abort if underflow
    }

    // add x and y, abort if overflows
    public fun add(x: Uint16, y: Uint16): Uint16 {
        let (lo, c) = underlying_add_with_carry(x.lo, y.lo);
        let hi = underlying_add_plus_carry(x.hi, y.hi, c);
        Uint16 {
            hi,
            lo,
        }
    }

    // subtract y from x, abort if underflows
    public fun subtract(x: Uint16, y: Uint16): Uint16 {
        let (lo, b) = underlying_sub_with_borrow(x.lo, y.lo);
        let hi = underlying_sub_minus_borrow(x.hi, y.hi, b);
        Uint16 {
            hi,
            lo,
        }
    }

    // x * y, first return value is the lower part of the result, second return value is the upper part of the result.
    fun underlying_mul_with_carry(x: u8, y: u8):(u8, u8) {
        // split x and y into lower part and upper part.
        // xh, xl, yh, yl
        // result is
        // upper = xh * xl + (xh * yl) >> half_size + (xl * yh) >> half_size
        // lower = xl * yl + (xh * yl) << half_size + (xl * yh) << half_size
        let xh = (x & UNDERLYING_UPPER_ONES) >> UNDERLYING_HALF_SIZE;
        let xl = x & UNDERLYING_LOWER_ONES;
        let yh = (y & UNDERLYING_UPPER_ONES) >> UNDERLYING_HALF_SIZE;
        let yl = y & UNDERLYING_LOWER_ONES;
        let xhyl = xh * yl;
        let xlyh = xl * yh;
        let hi = xh * yh + (xhyl >> UNDERLYING_HALF_SIZE) + (xlyh >> UNDERLYING_HALF_SIZE);
        let lo = xl * yl + ((xhyl & UNDERLYING_LOWER_ONES) << UNDERLYING_HALF_SIZE) + ((xlyh & UNDERLYING_LOWER_ONES)<< UNDERLYING_HALF_SIZE);
        (lo, hi)
    }

    // x * y, abort if overflow
    public fun multiply(x: Uint16, y: Uint16): Uint16 {
        assert!(x.hi * y.hi == 0, E_OVERFLOW); // if hi * hi is not zero, overflow already
        let (xlyl, xlyl_carry) = underlying_mul_with_carry(x.lo, y.lo);
        Uint16 {
            lo: xlyl,
            hi: x.lo * y.hi + x.hi * y.lo + xlyl_carry,
        }
    }

    // left shift, abort if shift is greater than the size of the int.
    public fun lsh(x: Uint16, y: u8): Uint16 {
        assert!(y <= FULL_SIZE, E_OVERFLOW);
        if (y >= UNDERLYING_SIZE) {
            Uint16 {
                hi: x.lo << (y - UNDERLYING_SIZE),
                lo: 0,
            }
        } else if (y == 0) {
            copy x
        } else {
            Uint16 {
                hi: (x.hi << y) + (x.lo >> (UNDERLYING_SIZE - y)),
                lo: x.lo << y,
            }
        }
    }

    // right shift, abort if shift is greater than the size of the int
    public fun rsh(x: Uint16, y: u8): Uint16 {
        assert!(y <= FULL_SIZE, E_OVERFLOW);
        if (y >= UNDERLYING_SIZE) {
            Uint16 {
                hi: 0,
                lo: x.hi >> (y - UNDERLYING_SIZE),
            }
        } else if (y==0) {
            copy x
        } else {
            Uint16 {
                hi: x.hi >> y,
                lo: (x.lo >> y) + (x.hi << (UNDERLYING_SIZE-y)),
            }
        }
    }

    fun underlying_leading_zeros(x: u8): u8 {
        if (x == 0) {
            UNDERLYING_SIZE
        } else {
            let n: u8 = 0;
            let k: u8 = UNDERLYING_HALF_POINT;
            while ((k & x) == 0) {
                n = n + 1;
                k = k >> 1;
            };

            n
        }
    }

    public fun leading_zeros(x: Uint16): u8 {
        if (x.hi == 0) {
            UNDERLYING_SIZE + underlying_leading_zeros(x.lo)
        } else {
            underlying_leading_zeros(x.hi)
        }
    }

    fun add_underlying(x:Uint16, y: u8): Uint16 {
        let (lo, carry) = underlying_add_with_carry(x.lo, y);
        Uint16 {
            lo,
            hi: x.hi + carry,
        }
    }
    // divide_mod returns x/y and x%y
    public fun divide_mod(x: Uint16, y: Uint16): (Uint16, Uint16) {
        assert!(y.hi != 0 || y.lo != 0, E_DIVISION_BY_ZERO);
        if (greater(y, x)) {
            (zero(), copy x)
        } else if (equal(x, y)){
            (new(0,1), zero())
        } else if (x.hi == 0 && y.hi == 0) {
            (new(0, x.lo/y.lo), new(0,x.lo%y.lo))
        } else {
            let n_x = leading_zeros(x);
            let n_y = leading_zeros(y);
            // x is greater than y, so this cannot happen
            // assert!(n_x <= n_y, 1);

            let shift = n_y - n_x;
            let current = lsh(y, shift);
            let reminder = copy x;

            let result = new(0,0);

            loop {
                if (!less(reminder, current)) {
                    reminder = subtract(reminder, current);
                    result = add(result, lsh(new(0,1), shift));
                };
                if (shift == 0) { break };
                current = rsh(current, 1);
                shift = shift - 1;
            };

            (result, reminder)
        }
    }

    public fun divide(x: Uint16, y: Uint16): Uint16 {
        let (r, _) = divide_mod(x, y);
        r
    }

    public fun mod(x: Uint16, y: Uint16): Uint16 {
        let (_, r) = divide_mod(x, y);
        r
    }

    const E_WRONG_RESULT: u64 = 1001;
    #[test]
    fun test_uint16() {
        let (r, carry) = underlying_add_with_carry(252, 9);
        assert!(r == 5, E_WRONG_RESULT);
        assert!(carry == 1, E_WRONG_RESULT);
        let (r, borrow) = underlying_sub_with_borrow(30, 45);
        assert!(r == 241, E_WRONG_RESULT);
        assert!(borrow == 1, E_WRONG_RESULT);
        let mul_r = multiply(new(0,255), new(0,2));
        assert!(equal(mul_r, new(1,254)), (mul_r.lo as u64));
        assert!(equal(lsh(new(6,UNDERLYING_UPPER_ONES),2),new(27,192)), E_WRONG_RESULT);
        let (div, rm) = divide_mod(new(83, 97),new(1, 200));
        assert!(equal(div, new(0,46)), (div.hi as u64));
        assert!(equal(rm, new(1, 113)), (rm.lo as u64));
    }
}